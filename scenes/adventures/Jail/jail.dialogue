# cutscene
~ intro_cutscene
: Ти приходиш до міської в’язниці. Заходячи усередину, ти бачиш затхле підвальне приміщення, що налічує усього чотири камери. Посередині зали стоїть закинутий гральними картами та напівпустими кухлями стіл. [ID:ТИ_ПРИХОДИШ_ДО_МІСЬКОЇ_В_ЯЗНИЦ_A3FF6A]
: В іншому кінці зали ти бачиш двох вартових, які зібрались навколо камери, що, на відміну від усіх інших, була закрита. Приглядаючись, ти помічаєш, що за ґратами знаходиться ще один розгублений вартовий. [ID:В_ІНШОМУ_КІНЦІ_ЗАЛИ_ТИ_БАЧИШ_Д_74AD1C]
Марк Грінвуд: Панове, на мою думку, план з пошуком ключів офіційно можна вважати провальним. Хочу внести нову пропозицію на загальне обговогення! Пропоную різати прути! [ID:ПАНОВЕ_НА_МОЮ_ДУМКУ_ПЛАН_З_ПОШ_1A7281]
Велдрік Грінвуд: Здугів? Нам пгоблем потім не обібгатися! [ID:ЗДУГІВ_НАМ_ПГОБЛЕМ_ПОТІМ_НЕ_ОБ_45FC58] 
Девіс: Ви маєте зробити вже хоч щось, благаю… [ID:ВИ_МАЄТЕ_ЗРОБИТИ_ВЖЕ_ХОЧ_ЩОСЬ_5FE730]
=> intro
=> END

~ intro
: Ви швидко усвідомлюєте, що за цими розмовами вартові не встигли Вас помітити. Що зробите? [ID:ВИ_ШВИДКО_УСВІДОМЛЮЄТЕ_ЩО_ЗА_Ц_132444]
- Привітатись [ID:ПРИВІТАТИСЬ_532EFB]
	Олаф: Вітаю! Дуже не хотілося б вас відривати, але у мене термінова справа… [ID:ВІТАЮ_ДУЖЕ_НЕ_ХОТІЛОСЯ_Б_ВАС_В_6752B0]
	Велдрік Грінвуд: Олафе! Дідько, ти хоч знаєш, що накоїв?! І куди ти взагалі вночі пгопав?! [ID:ОЛАФЕ_ДІДЬКО_ТИ_ХОЧ_ЗНАЄШ_ЩО_Н_C51D46]
- Гучно відхаркнути і сплюнути [ID:ГУЧНО_ВІДХАРКНУТИ_І_СПЛЮНУТИ_131293]
	Велдрік Грінвуд: Хто це тут…? Олафе, це ти? Дідько, ти хоч знаєш, що накоїв?! І куди ти взагалі вночі пгопав?! [ID:ХТО_ЦЕ_ТУТ_ОЛАФЕ_ЦЕ_ТИ_ДІДЬКО_514EEB]
- Чекати, поки вони самі Вас помітять [ID:ЧЕКАТИ_ПОКИ_ВОНИ_САМІ_ВАС_ПОМ_7E846A]
	: Пройшло пару хвилин, і один з вартових відривається від розглядання свого друга у камері. Помітивши Вас, він перелякано засіпав плечима. [ID:ПРОЙШЛО_ПАРУ_ХВИЛИН_І_ОДИН_З_В_4C29D5]
	Велдрік Грінвуд: А-а! Олафе, дідько! Налякав. Як довго ти тут стоїш?! Хоча ні, інше питання. Ти хоч знаєш, що накоїв?! [ID:А_А_ОЛАФЕ_ДІДЬКО_НАЛЯКАВ_ЯК_ДО_D73848]
Олаф: І чому тільки всі зустрічають мене саме так..? [ID:І_ЧОМУ_ТІЛЬКИ_ВСІ_ЗУСТРІЧАЮТЬ_3BA6FA] 
Марк Грінвуд: Маю визнати, тобі неймовірно вдало вдається примножувати хаос, дварфе! Боюся здатися нетактовним, але можливо варто пам’ятати про міру, коли справа стосується алкоголю. [ID:МАЮ_ВИЗНАТИ_ТОБІ_НЕЙМОВІРНО_ВД_C0E9F7]
Олаф: Так-так. Але я тут по дуже важливій справі. Ми з вами вочевидь випивали вночі разом, і мені дуже важливо дізнатись, де… [ID:ТАК_ТАК_АЛЕ_Я_ТУТ_ПО_ДУЖЕ_ВАЖЛ_273CA7]
Велдрік Грінвуд: Вочевидь? То ти що, хочеш сказати, що не пам’ятаєш цього? [ID:ВОЧЕВИДЬ_ТО_ТИ_ЩО_ХОЧЕШ_СКАЗАТ_44140B]
Олаф:  Е-е-е, так. Я продовжу. Вчора зі мною була золота скриня, яку мені тепер дуже треба знайти. Може ви бачили її? У пригоді буде будь-яка інформація. [ID:Е_Е_Е_ТАК_Я_ПРОДОВЖУ_ВЧОРА_ЗІ_16FF39]
Марк Грінвуд: Дійсно, з тобою така була. [ID:ДІЙСНО_З_ТОБОЮ_ТАКА_БУЛА_E81C4F]
- Справді? Нарешті, зачіпка! [ID:СПРАВДІ_НАРЕШТІ_ЗАЧІПКА_E47598]
	Велдрік Грінвуд: Так! Але ми тобі більше нічого не скажемо, бо ти нас з бгатами жахливо підставив! [ID:ТАК_АЛЕ_МИ_ТОБІ_БІЛЬШЕ_НІЧОГО_1CE441]
- Ану викладай все, що знаєш, вартовичку! [ID:АНУ_ВИКЛАДАЙ_ВСЕ_ЩО_ЗНАЄШ_ВАР_5455A8]
	Велдрік Грінвуд: Ось ти як заговогив? Дзуськи! Ми тобі нічого не скажемо. Ти нас з бгатами жахливо підставив! [ID:ОСЬ_ТИ_ЯК_ЗАГОВОГИВ_ДЗУСЬКИ_МИ_18D89D]
- І ви, звичайно, мені допоможете її знайти! Так? [ID:І_ВИ_ЗВИЧАЙНО_МЕНІ_ДОПОМОЖЕТЕ_E5B4A5]
	Велдрік Грінвуд: Ха! Дзуськи! Ми не будемо тобі допомагати! Ти нас з бгатами жахливо підставив! [ID:ХА_ДЗУСЬКИ_МИ_НЕ_БУДЕМО_ТОБІ_Д_31F99D]
Марк Грінвуд: Велдрік якоюсь мірою правий. З нашого боку буде негідно проковтнути твою зраду і допомагати так, ніби нічого не сталося. Будь це хто завгодно інший, я б невідкладно допоміг. Але ти… Вибач, та це було занадто. [ID:ВЕЛДРІК_ЯКОЮСЬ_МІРОЮ_ПРАВИЙ_З_409284]
- Поясніть мені все розмірено і детально! [ID:ПОЯСНІТЬ_МЕНІ_ВСЕ_РОЗМІРЕНО_І_910759]
	Велдрік Грінвуд: Що сталося?! Бачиш погожні камери? Це твоя гобота! Коли Тогвін викликав нас у когчму, ми мали тебе закгити за гхатами. [ID:ЩО_СТАЛОСЯ_БАЧИШ_ПОГОЖНІ_КАМЕР_69207E]
	Велдрік Грінвуд: Але ти здавався зовсім непоганим двагфом і запгопонував замість цього погхати в кагти й випити газом. І все було добге, аж поки ти не запгосив доєднатись до гхи наших в’язнів. [ID:АЛЕ_ТИ_ЗДАВАВСЯ_ЗОВСІМ_НЕПОГАН_2F9CEA]
	Марк Грінвуд: На превелику шкоду, ми були вже надто п’яні, щоб зважити всі “за” і “проти” та побачити недоліки самої цієї концепції… [ID:НА_ПРЕВЕЛИКУ_ШКОДУ_МИ_БУЛИ_ВЖЕ_D67125]
	Велдрік Грінвуд: Магку, не починай! Це все одно пговина Олафа. Спочатку ці бовдуги дійсно гхали з нами! Та потім, коли ми відірвалися на пошук додаткової випивки, ці тгоглодити – гопа! – скогистались моментом і втекли. [ID:МАГКУ_НЕ_ПОЧИНАЙ_ЦЕ_ВСЕ_ОДНО_П_232EFE]
	Велдрік Грінвуд: І знаєш, що ти згобив, коли ми попгосили тебе пго допомогу? Так само як і вони! Здгиснув! На твій погляд, це ногмально? [ID:І_ЗНАЄШ_ЩО_ТИ_ЗГОБИВ_КОЛИ_МИ_П_07D89E]
	Девіс: Ви й про мене не маєте забувати! Мені окремо дісталося. Ти, Олафе, вирішив наді мною пожартувати та закрив тут! Сказав, що я тепер заступник в’язнів, і втік. [ID:ВИ_Й_ПРО_МЕНЕ_НЕ_МАЄТЕ_ЗАБУВАТ_EE2440]
	Девіс: А де ключі – це тепер ніхто не знає! Я тут всю ніч і півдня просидів… [ID:А_ДЕ_КЛЮЧІ_ЦЕ_ТЕПЕР_НІХТО_НЕ_З_1835CD] 
- Та кажіть вже, що ж такого сталося? Тільки коротко! [ID:ТА_КАЖІТЬ_ВЖЕ_ЩО_Ж_ТАКОГО_СТА_B58E86]
	Велдрік Грінвуд: Тогвін викликав тебе нас у когчму, щоб ми тебе забгали. Та по догозі ми здгужилися. Пгийшли у в’язницю, сіли газом випивати і гхати в кагти. [ID:ТОГВІН_ВИКЛИКАВ_ТЕБЕ_НАС_У_КОГ_4AEB12]
	Велдрік Грінвуд: Та ти випустив усіх в’язнів, мовляв, вони хочуть з нами погхати. У гезультаті вони втекли! А вслід за ними і ти! [ID:ТА_ТИ_ВИПУСТИВ_УСІХ_В_ЯЗНІВ_МО_98905C] 
	Девіс: Мені теж дісталося! Ти, Олафе, вирішив наді мною пожартувати та закрив тут! Я тут всю ніч і півдня просидів… [ID:МЕНІ_ТЕЖ_ДІСТАЛОСЯ_ТИ_ОЛАФЕ_ВИ_5470B7]
Марк Грінвуд: Тепер розумієш? Ти серйозно скривдив мене і моїх братів. Хочеш отримати інформацію про скриню, виправ помилки, що наробив. Це найдобросердніша пропозиція, яку я можу зробити. [ID:ТЕПЕР_РОЗУМІЄШ_ТИ_СЕРЙОЗНО_СКР_84FC0D]
- Ви дійсно хочете, щоб я ловив злочинців? Які втекли ще вночі? Та я їх ніколи не знайду! [ID:ВИ_ДІЙСНО_ХОЧЕТЕ_ЩОБ_Я_ЛОВИВ_242FF1]
- Фух! Нарешті зможу на когось виплеснути накопичену за цей чортовий день агресію! [ID:ФУХ_НАРЕШТІ_ЗМОЖУ_НА_КОГОСЬ_В_94D22B]
	Марк Грінвуд: Тож ти згоден? [ID:ТОЖ_ТИ_ЗГОДЕН_38891F]
- А пропозицію руки й серця не зробиш?.. Ха-ха. Гаразд. Це жарт. Не роби, будь ласка. [ID:А_ПРОПОЗИЦІЮ_РУКИ_Й_СЕРЦЯ_НЕ_72DBB6]
	Марк Грінвуд: Щоб спростити нашу подальшу комунікацію, я удам, що не чув цього. [ID:ЩОБ_СПРОСТИТИ_НАШУ_ПОДАЛЬШУ_КО_C3F042]
Марк Грінвуд: Для початку, спробуй знайти ключі від камери, у якій запертий Девіс. Як розберемося з цим, я підкажу, яким чином ми зможемо знайти в’язнів, гаразд? [ID:ДЛЯ_ПОЧАТКУ_СПРОБУЙ_ЗНАЙТИ_КЛЮ_59FC87]
Олаф: Гаразд. Де саме шукати? [ID:ГАРАЗД_ДЕ_САМЕ_ШУКАТИ_4FA753]
Девіс: Після того, як ти запер мене тут, ти бігав всією в’язницею і реготав, як скажений. Це останній раз, коли хтось з нас бачив ключі. [ID:ПІСЛЯ_ТОГО_ЯК_ТИ_ЗАПЕР_МЕНЕ_ТУ_DAFCD7]
Девіс: Тому пошукай десь по кутках, вони могли випасти у тебе з рук під час твоєї біганини. Ти маєш мене звільнити, я вже не витримую сидіти на цій холодній підлозі! А стояти вже ноги болять! [ID:ТОМУ_ПОШУКАЙ_ДЕСЬ_ПО_КУТКАХ_ВО_1A6B04]
Марк Грінвуд: Почекай ще трохи, брате. Олаф зробить все, що від нього залежить. Так, Олафе? [ID:ПОЧЕКАЙ_ЩЕ_ТРОХИ_БРАТЕ_ОЛАФ_ЗР_C4F050]
- Егеж. [ID:ЕГЕЖ_E61052]
- І як такі ніженки стають вартовими? [ID:І_ЯК_ТАКІ_НІЖЕНКИ_СТАЮТЬ_ВАРТ_07F478]
- Терпи, Олафе… Коли-небудь настане день, і від тебе не залежитиме нічого… [ID:ТЕРПИ_ОЛАФЕ_КОЛИ_НЕБУДЬ_НАСТА_327E79]
=> END

~ keys_found
: Ви приносите ключі братам. Марк швидко відкриває замок, Девіс підхоплюється з місця і щасливо вискакує з камери. [ID:ВИ_ПРИНОСИТЕ_КЛЮЧІ_БРАТАМ_МАРК_B3770A] 
Девіс: Ну нарешті! Мені майже хочеться тобі подякувати, Олафе. Але все ж таки я не маю цього робити. Враховуючи, що це ти мене тут і запер. [ID:НУ_НАРЕШТІ_МЕНІ_МАЙЖЕ_ХОЧЕТЬСЯ_517C0A]
Олаф: Як тобі буде завгодно. [ID:ЯК_ТОБІ_БУДЕ_ЗАВГОДНО_BBBE49] 
Марк Грінвуд: Що ж, тепер в’язні. Я допоможу тобі знаннями, Олафе. Наскільки мені відомо, усі троє втікачів ще ховаються у катакомбах під в’язницею. Ми не повідомляли про це іншим вартовим, бо у нас були б серйозні проблеми. [ID:ЩО_Ж_ТЕПЕР_В_ЯЗНІ_Я_ДОПОМОЖУ_Т_B8920D]
Марк Грінвуд: Сподівались розібратись з цим самі, але надто довго шукали ключі від камерии Девіса. Через годину прийдуть чергові на планову перевірку. До того моменту ти маєш знайти всіх трьох в’язнів. [ID:СПОДІВАЛИСЬ_РОЗІБРАТИСЬ_З_ЦИМ_180819] 
Велдрік Грінвуд: Я гозкажу пго особливості кожного з них. [ID:Я_ГОЗКАЖУ_ПГО_ОСОБЛИВОСТІ_КОЖН_649CB1] 
Марк Грінвуд: Велдріку? Давай краще я? [ID:ВЕЛДРІКУ_ДАВАЙ_КРАЩЕ_Я_499417]
Велдрік Грінвуд: Чому? [ID:ЧОМУ_581ED1]
Марк Грінвуд: Ну… Щоб тебе не напружувати. [ID:НУ_ЩОБ_ТЕБЕ_НЕ_НАПРУЖУВАТИ_91CB9A]
=> prisoner_selection
=> END

~ prisoner_selection
Марк Грінвуд: Кого зараз підеш шукати, Олафе? [ID:КОГО_ЗАРАЗ_ПІДЕШ_ШУКАТИ_ОЛАФЕ_59F171]
- Варраса Чорного Кігтя [if varras_returned == false] [ID:ВАРРАСА_ЧОРНОГО_КІГТЯ_IF_VARR_33AC4D]
	Марк Грінвуд: Варрас Чорний Кіготь відомий своєю руйнівною силою, яку використовує, щоб ламати все на своєму шляху. Будь обережним, коли підеш його шукати! [ID:ВАРРАС_ЧОРНИЙ_КІГОТЬ_ВІДОМИЙ_С_172871]
	set prison_active_prisoner = "varras"
- Драгара Костогриза [if dragarh_returned == false] [ID:ДРАГАРА_КОСТОГРИЗА_IF_DRAGARH_722F94]
	Марк Грінвуд: Драгар Костогриз. Він досвідчений розбійник і вміло зливається з оточенням. Тож тобі доведеться бути пильним, щоб зловити його. [ID:ДРАГАР_КОСТОГРИЗ_ВІН_ДОСВІДЧЕН_595345]
	set prison_active_prisoner = "dragarh"
- Шайру Рівенбо [if shaira_returned == false] [ID:ШАЙРУ_РІВЕНБО_IF_SHAIRA_RETUR_7D15BE]
	Марк Грінвуд: Шайра Рівенбо. Вона майстриня-механік, яка з будь-чого ладна зробити пастку, тому бережись її розробок. [ID:ШАЙРА_РІВЕНБО_ВОНА_МАЙСТРИНЯ_М_97A195]
	set prison_active_prisoner = "shaira"
=> END

~ prisoners_returned
: Ви повертаєтеся захекані і втомлені, але нарешті всі три втікачі на місці. В’язниця за цей час стала чистішою. Здається брати часу не гаяли. [ID:ВИ_ПОВЕРТАЄТЕСЯ_ЗАХЕКАНІ_І_ВТО_CA1EB8]
: Велдрік, першим помітивши Вас і вашу компанію, спішить перехопити злочинців, щоб повернути їх у камери. Марк підходить з щирою посмішкою і кладе руку на твоє плече, хоч для цього йому й приходиться трохи схилитись. [ID:ВЕЛДРІК_ПЕРШИМ_ПОМІТИВШИ_ВАС_І_429551] 
Марк Грінвуд: Гарна робота, Олафе! Маю визнати, ти вмієш вражати контроверсійними гранями свого характеру. [ID:ГАРНА_РОБОТА_ОЛАФЕ_МАЮ_ВИЗНАТИ_C8E9E5]
Олаф: Якими? [ID:ЯКИМИ_DF4F47]
Марк Грінвуд: Контроверсійними. Фух! Як добре, що тепер всі на місцях! Якби черговий прийшов раніше вашого повернення, і король би прознав про те, що сталося, у нас були б колосальні проблеми! [ID:КОНТРОВЕРСІЙНИМИ_ФУХ_ЯК_ДОБРЕ_C56077]
Марк Грінвуд: Він весь день ходить і лютує. Служниці кажуть, скаржиться на болі в животі, от і ходить злий. Може це дає збій нервова система перед коронацією? [ID:ВІН_ВЕСЬ_ДЕНЬ_ХОДИТЬ_І_ЛЮТУЄ_С_85430D]
Олаф: Та хто його знає… [ID:ТА_ХТО_ЙОГО_ЗНАЄ_D09343]
Велдрік Грінвуд: Ото ж бо, слухай, що було вчога, Олафе. Бо Магк тебе загаз усіма чутками закидає, а до спгави так і не дійдемо… Ми тобі не все гозповіли. [ID:ОТО_Ж_БО_СЛУХАЙ_ЩО_БУЛО_ВЧОГА_7EE715]
Велдрік Грінвуд: Вчога з нами в кагти гхали не тільки в’язні, а ще й конюх, на ім'я Томас Ельсбегн. [ID:ВЧОГА_З_НАМИ_В_КАГТИ_ГХАЛИ_НЕ_1AA321]
Велдрік Грінвуд: На нашому шляху до в’язниці ти його підчепив, запгопонував з нами піти, щоб газом в кагти погхати. Він і погодився. Хогоший чоловік виявився, веселий. [ID:НА_НАШОМУ_ШЛЯХУ_ДО_В_ЯЗНИЦІ_ТИ_BF65EF] 
Марк Грінвуд: Але коли ми зрозуміли, що в’язні зникли і їх треба шукати, ви разом з Томасом у мить утекли! [ID:АЛЕ_КОЛИ_МИ_ЗРОЗУМІЛИ_ЩО_В_ЯЗН_34F1C8]
Марк Грінвуд: Вже й не знаю, що там було далі, але, якщо хочеш відшукати ту скриню, піди до нього у стайні й спитайся. Може він підкаже щось корисне. [ID:ВЖЕ_Й_НЕ_ЗНАЮ_ЩО_ТАМ_БУЛО_ДАЛІ_76930D] 
- Томас Ельсберн, кажеш? Добре, пошукаю його. [ID:ТОМАС_ЕЛЬСБЕРН_КАЖЕШ_ДОБРЕ_ПО_C9955D]
- Знову лишень наводіння і жодної конкретної інформації. Мені починає це трохи набридати… [ID:ЗНОВУ_ЛИШЕНЬ_НАВОДІННЯ_І_ЖОДН_F049B4]
- Ліпше б мене вже просто посадили за ґрати… [ID:ЛІПШЕ_Б_МЕНЕ_ВЖЕ_ПРОСТО_ПОСАД_7CDADD]
Марк Грінвуд: І ще. Олафе? Маєш ще хвилинку? Хочу дещо наостанок сказати. [ID:І_ЩЕ_ОЛАФЕ_МАЄШ_ЩЕ_ХВИЛИНКУ_ХО_1D122C]
- Так? [ID:ТАК_4B0B0E]
	Марк Грінвуд: Хочу тебе попросити більше не влаштовувати такі руйнівні вечори у корчмі Торвіна. Його дуже задіває, коли з “Рогатим щуром” поводяться негідно. [ID:ХОЧУ_ТЕБЕ_ПОПРОСИТИ_БІЛЬШЕ_НЕ_650B9F]
	Марк Грінвуд: Для нього корчма священніша за будь-який храм. Псувати її рівносильно тому, щоб псувати пам'ять Ізольди. [ID:ДЛЯ_НЬОГО_КОРЧМА_СВЯЩЕННІША_ЗА_6D50BB]
	Олаф: Я, здається, вже чув про неї… [ID:Я_ЗДАЄТЬСЯ_ВЖЕ_ЧУВ_ПРО_НЕЇ_4FB0B2]
	Марк Грінвуд: Торвін з Ізольдою були близькими друзями наших батьків. Вони були нам майже як рідні дядько і тітка, та все змінилось після того, як Ізольда померла. З тих часів Торвін майже не виходить з Щура. [ID:ТОРВІН_З_ІЗОЛЬДОЮ_БУЛИ_БЛИЗЬКИ_79C0DE] 
	Олаф: Що сталося? [ID:ЩО_СТАЛОСЯ_2C95BA] 
	Марк Грінвуд: Бійка. Якісь розбійники прийшли випити до Щура. Вони ховали обличчя, та Ізольда випадково побачила одного з них, і вони напали. У той вечір взагалі багато хто постраждав. Але тільки Ізольда спочила. [ID:БІЙКА_ЯКІСЬ_РОЗБІЙНИКИ_ПРИЙШЛИ_ED6B79]
	Олаф: І як Торвін не закрив корчму після такого? [ID:І_ЯК_ТОРВІН_НЕ_ЗАКРИВ_КОРЧМУ_П_A92118] 
	Марк Грінвуд: Ізольда взяла з нього слово, що він збереже їх справу, не дивлячись ні на що. [ID:ІЗОЛЬДА_ВЗЯЛА_З_НЬОГО_СЛОВО_ЩО_12F8BC]
	Олаф: Я почув тебе, Марку. Я подумаю над цим. [ID:Я_ПОЧУВ_ТЕБЕ_МАРКУ_Я_ПОДУМАЮ_Н_EF1E01]
- Вибач, я поспішаю [ID:ВИБАЧ_Я_ПОСПІШАЮ_B76D32]
	Марк Грінвуд: Що ж, розумію. [ID:ЩО_Ж_РОЗУМІЮ_52E16D] 
Олаф: У будь-якому разі, дякую, хлопці! І вибачте за розгардіяш, сподіваюсь, у вас не буде проблем через мене. [ID:У_БУДЬ_ЯКОМУ_РАЗІ_ДЯКУЮ_ХЛОПЦІ_2A870C]
Марк Грінвуд: Дякую, Олафе, і бувай. Розкажеш потім, чим все скінчилось. [ID:ДЯКУЮ_ОЛАФЕ_І_БУВАЙ_РОЗКАЖЕШ_П_3FEB6E]
Олаф: Або я розкажу, або дізнаєтесь зі своїх звітів про тих, кого стратили… [ID:АБО_Я_РОЗКАЖУ_АБО_ДІЗНАЄТЕСЬ_З_25154F]
=> END

~ imprisoning
: Ви прокидаєтеся у в'язниці. Голова болить, але ніби всі кістки цілі. Крізь ґрати видно знайомих тобі вартових. Троє братів грають в карти за столом і п'ють щось з кухлів. [ID:ВИ_ПРОКИДАЄТЕСЯ_У_В_ЯЗНИЦІ_ГОЛ_45342F] 
Олаф: Марку, хлопці! [ID:МАРКУ_ХЛОПЦІ_A41901] 
: Вартові кидають карти на стіл і повертаються до Вас. Марк підходить до Вашої камери. [ID:ВАРТОВІ_КИДАЮТЬ_КАРТИ_НА_СТІЛ_AB3674]
Марк Грінвуд: Олаф! Ти прокинувся? Як почуваєшся? [ID:ОЛАФ_ТИ_ПРОКИНУВСЯ_ЯК_ПОЧУВАЄШ_F60D0A]
Олаф: Так, ніби у мене друге похмілля за день. Що трапилось? [ID:ТАК_НІБИ_У_МЕНЕ_ДРУГЕ_ПОХМІЛЛЯ_10D6E0]
Марк Грінвуд: Тебе схопила королівська гвардія за махінації з виготовленням королівського замовлення. А разом з тобою і твоїх друзів. [ID:ТЕБЕ_СХОПИЛА_КОРОЛІВСЬКА_ГВАРД_AC7DD4] 
- Моїх друзів? [ID:МОЇХ_ДРУЗІВ_EAFFD2]
	Марк Грінвуд: Так, Торвіна… [ID:ТАК_ТОРВІНА_1A3726]
	: Ви чуєте знайомий голос з-за стіни. [ID:ВИ_ЧУЄТЕ_ЗНАЙОМИЙ_ГОЛОС_З_ЗА_С_8F6605]
	Торвін Круторіг: З пробудженням, друже. [ID:З_ПРОБУДЖЕННЯМ_ДРУЖЕ_D514BF]
	Марк Грінвуд: …якусь наднормально сильну жінку, не пам'ятаю її ім'я… [ID:ЯКУСЬ_НАДНОРМАЛЬНО_СИЛЬНУ_ЖІН_54F91C]
	Серена Морано: Серена, довбню! Привіт, Олафе! [ID:СЕРЕНА_ДОВБНЮ_ПРИВІТ_ОЛАФЕ_41F3D1]
	Марк Грінвуд: …І навіть графиню. [ID:І_НАВІТЬ_ГРАФИНЮ_4EEDFB]
	Софія-Амалія: Я, чесно кажучи, сподівалась, що мене не арештують. З тебе випивка, Олафе! [ID:Я_ЧЕСНО_КАЖУЧИ_СПОДІВАЛАСЬ_ЩО_FCA03A] 
	Марк Грінвуд: Не впевнений, що він здатний забезпечити вам алкоголь, пані. [ID:НЕ_ВПЕВНЕНИЙ_ЩО_ВІН_ЗДАТНИЙ_ЗА_82DEA8] 
- Не будемо витрачати час на балачки. Що зі мною буде? [ID:НЕ_БУДЕМО_ВИТРАЧАТИ_ЧАС_НА_БА_4EEB7A]
Марк Грінвуд: Мені дуже шкода, Олафе, але тебе мають стратити завтра на світанку. Торвіна і жі... Серену хочуть ув’язнити на рік чи може більше. [ID:МЕНІ_ДУЖЕ_ШКОДА_ОЛАФЕ_АЛЕ_ТЕБЕ_A52BE3]
Марк Грінвуд: І тільки графиню відпустять через два дні. Вона все ж таки аристократка. [ID:І_ТІЛЬКИ_ГРАФИНЮ_ВІДПУСТЯТЬ_ЧЕ_DA16E3]
- Як це так? [ID:ЯК_ЦЕ_ТАК_079BFF]
	Девіс: Мені дуже шкода, Олафе. [ID:МЕНІ_ДУЖЕ_ШКОДА_ОЛАФЕ_7472CE]
	Олаф: Бути не може! Треба щось зробити! [ID:БУТИ_НЕ_МОЖЕ_ТРЕБА_ЩОСЬ_ЗРОБИТ_AB6AAB]
- Випустіть мене негайно! А інакше я вам тут все розламаю! [ID:ВИПУСТІТЬ_МЕНЕ_НЕГАЙНО_А_ІНАК_8DD562]
	: Ви б'єте по ґратам. [ID:ВИ_Б_ЄТЕ_ПО_ҐРАТАМ_DD8B4C]
	Девіс: Ми не можемо, Олафе. [ID:МИ_НЕ_МОЖЕМО_ОЛАФЕ_7DD24B]
	Олаф: Зрадники! [ID:ЗРАДНИКИ_CFC598]
- Я теж хочу дводенний арешт! Графине, а можна я знову вас підміню? [ID:Я_ТЕЖ_ХОЧУ_ДВОДЕННИЙ_АРЕШТ_ГР_B78F2F]
	Софія-Амалія: Вибач, Олафе, та підозрюю, що вдруге цей план не спрацює… [ID:ВИБАЧ_ОЛАФЕ_ТА_ПІДОЗРЮЮ_ЩО_ВДР_BE63E2]
Велдрік Грінвуд: Пгобач, але вже нічого не вдіяти. Ви пготидіяли коголівській гвагдії. У такому випадку важко щось заподіяти. [ID:ПГОБАЧ_АЛЕ_ВЖЕ_НІЧОГО_НЕ_ВДІЯТ_D82F2D]
- Зачекайте-но, а що з короною? [ID:ЗАЧЕКАЙТЕ_НО_А_ЩО_З_КОРОНОЮ_CAA52E]
	Марк Грінвуд: Королівська рада терміново шукає спосіб її замінити. Я так зрозумів, сапфір, що пропав, це ритуальний камінь. Без нього коронація не буде визнана. Можливо, її прийдеться перенести, щоб було більше часу на пошуки. [ID:КОРОЛІВСЬКА_РАДА_ТЕРМІНОВО_ШУК_C10C2A]
	Марк Грінвуд: Може знайдуть підставний… Але будь-які подібні махінації можуть підірвати авторитет Його Величності. Тому я й кажу, шансів на помилування немає. [ID:МОЖЕ_ЗНАЙДУТЬ_ПІДСТАВНИЙ_АЛЕ_Б_D07224]
- Не турбувати вартових додатковими питаннями [ID:НЕ_ТУРБУВАТИ_ВАРТОВИХ_ДОДАТКО_419852]
Марк Грінвуд: Вибач, Олафе. Я, мабуть, піду до братів. Клич, якщо буде щось треба. [ID:ВИБАЧ_ОЛАФЕ_Я_МАБУТЬ_ПІДУ_ДО_Б_D79955]
: Ви безсило опускаєтеся на підлогу. Щось капає Вам на лоба. Ви торкаєтеся пальцями цього місця і підіймаєш голову. У камері навпроти сидять в’язні, яких Ви сьогодні ловили у катакомбах. Варрас, Драгар і Шайра. [ID:ВИ_БЕЗСИЛО_ОПУСКАЄТЕСЯ_НА_ПІДЛ_DEA1BA]
: Ви витираєте лоба від слини й відходите вглиб камери, щоб убезпечити себе від наступних спроб Вас дістати. Крізь металеві прути Вам видно, як брати повертаються до гри у карти. Хлопці виглядають пригніченими. [ID:ВИ_ВИТИРАЄТЕ_ЛОБА_ВІД_СЛИНИ_Й_4FA693] 
Олаф: Де ж може бути останній камінь? [ID:ДЕ_Ж_МОЖЕ_БУТИ_ОСТАННІЙ_КАМІНЬ_BD8364]
: Вам вже просто цікаво дізнатись. Ви помічаєте, як Велдрік, роздумуючи над наступним ходом у картковій грі, крутить на пальці обручку в різні сторони. [ID:ВАМ_ВЖЕ_ПРОСТО_ЦІКАВО_ДІЗНАТИС_A46512]
: У якийсь момент він надто сильно заграється і випадково скидає її у кухоль з напоєм. [ID:У_ЯКИЙСЬ_МОМЕНТ_ВІН_НАДТО_СИЛЬ_DC375A]
Велдрік Грінвуд: Йой! Втопив обгучку! Добге, що це лише кухоль, а не пагаша. Я чув і пго такі істогії. Мене дгужина за таке вбила б… [ID:ЙОЙ_ВТОПИВ_ОБГУЧКУ_ДОБГЕ_ЩО_ЦЕ_9FE3C8]
: І тут Вас пронизує раптовий спогад. [ID:І_ТУТ_ВАС_ПРОНИЗУЄ_РАПТОВИЙ_СП_1ED0F4]
=> END

~ escape
: Ви підскакуєте до ґратів і хапаєшся за прути. [ID:ВИ_ПІДСКАКУЄТЕ_ДО_ҐРАТІВ_І_ХАП_06258C] 
Олаф: Хлопці! Я згадав, куди подівався останній камінь! Я можу все виправити! Коронації ж ще не було? Скільки залишилось часу? [ID:ХЛОПЦІ_Я_ЗГАДАВ_КУДИ_ПОДІВАВСЯ_A52324] 
Марк Грінвуд: Десь година. Але, Олафе, боюся вже надто пізно. [ID:ДЕСЬ_ГОДИНА_АЛЕ_ОЛАФЕ_БОЮСЯ_ВЖ_83B863]
Олаф: Ні! Ні! Послухайте! Мені лише треба поговорити з королем! Ви можете його сюди привести? [ID:НІ_НІ_ПОСЛУХАЙТЕ_МЕНІ_ЛИШЕ_ТРЕ_BDFA21] 
Велдрік Грінвуд: Він нас не послухає, ми звичайні вагтові. [ID:ВІН_НАС_НЕ_ПОСЛУХАЄ_МИ_ЗВИЧАЙН_C468EB]
Олаф: Тоді… Випустіть мене! Я сам до нього прийду. Обіцяю, все буде добре. [ID:ТОДІ_ВИПУСТІТЬ_МЕНЕ_Я_САМ_ДО_Н_F13D5A] 
Марк Грінвуд: Я не можу ризикувати життями своїх братів, Олафе, вибач. [ID:Я_НЕ_МОЖУ_РИЗИКУВАТИ_ЖИТТЯМИ_С_BCF53B]
=> END

~ escape_composure
Девіс: Ні, зачекай-но, Марку! Олаф вже один раз довів, що вміє тримати слово. І вміє виправляти помилки. Не на п’яну голову, звичайно… Але ж зараз він тверезий! [ID:НІ_ЗАЧЕКАЙ_НО_МАРКУ_ОЛАФ_ВЖЕ_О_1DEB04] 
Олаф: Кудись не туди зайшла ця промова… [ID:КУДИСЬ_НЕ_ТУДИ_ЗАЙШЛА_ЦЯ_ПРОМО_EE1772]
Девіс: Я намагаюсь сказати, що вірю йому! [ID:Я_НАМАГАЮСЬ_СКАЗАТИ_ЩО_ВІРЮ_ЙО_39DC71]
Велдрік Грінвуд: І я! [ID:І_Я_02BD79]
: Марк зважує “за” і “проти” та важко видихає. [ID:МАРК_ЗВАЖУЄ_ЗА_І_ПРОТИ_ТА_ВАЖК_4AB3E9] 
Марк Грінвуд: Гаразд. [ID:ГАРАЗД_CCD693]
: Він підходить до Ваших ґрат і відкриває замок. [ID:ВІН_ПІДХОДИТЬ_ДО_ВАШИХ_ҐРАТ_І_FDFFF5]
Марк Грінвуд: Ми ризикуємо своїм життям, Олафе. Тож я дуже тебе прошу, будь обережний! [ID:МИ_РИЗИКУЄМО_СВОЇМ_ЖИТТЯМ_ОЛАФ_E68026]
Марк Грінвуд: Як ти вже знаєш, тут є катакомби. Вони з’єднані у тому числі з королівським замком. Я дам карту, щоб ти не загубився. [ID:ЯК_ТИ_ВЖЕ_ЗНАЄШ_ТУТ_Є_КАТАКОМБ_9AD2CC]
Марк Грінвуд: Далі треба буде пройди повз вартових у замку. Для цього скористайся таємним шляхом, я відмічу на карті, як до нього попасти. [ID:ДАЛІ_ТРЕБА_БУДЕ_ПРОЙДИ_ПОВЗ_ВА_BC57C4]
Марк Грінвуд: Ним не користувались вже багато років, то шлях може бути у занедбаному стані. Але ти постарайся, добре? [ID:НИМ_НЕ_КОРИСТУВАЛИСЬ_ВЖЕ_БАГАТ_1C6F6B]
: Ви виходите з камери й міцно обіймаєш братів. [ID:ВИ_ВИХОДИТЕ_З_КАМЕРИ_Й_МІЦНО_О_043682]
Олаф: Обіцяю. Торвіне, Серено, графине! Почекайте трохи. Я обов’язково визволю вас звідси! [ID:ОБІЦЯЮ_ТОРВІНЕ_СЕРЕНО_ГРАФИНЕ_25CB51]
=> END

~ escape_harshness
: Брати понуро відвертаються. Вони й самі не раді Вам відмовляти, але не готові так сильно ризикувати. Ви важко видихаєте і приглядаєшся до ґрат. Вони старі, ледве стоять. [ID:БРАТИ_ПОНУРО_ВІДВЕРТАЮТЬСЯ_ВОН_A13923]
: Недовго думаючи, Ви вириваєте одну з арматур, намагаєшся пролізти крізь утворену діру, але живіт заважає. Вартові підскакують від шуму. [ID:НЕДОВГО_ДУМАЮЧИ_ВИ_ВИРИВАЄТЕ_О_38F36C]
Велдрік Грінвуд: Олафе, що ти гобиш?! [ID:ОЛАФЕ_ЩО_ТИ_ГОБИШ_BAA9F3] 
: Тоді Ви дивитеся на замок, що висить на ґратах. Як і металеві прути, він здається не надто надійним. Зручно перехопивши арматуру у руках, Ви розбивате нею замок. [ID:ТОДІ_ВИ_ДИВИТЕСЯ_НА_ЗАМОК_ЩО_В_72EFE1] 
Марк Грінвуд: Зупинись! [ID:ЗУПИНИСЬ_8CA382]
Олаф: Не бійтеся, хлопці! Якщо у мене не вийде, скажете, що я втік! Зламані ґрати доведуть, що це просто в’язницю треба оновити, і ви ні в чому не винні. [ID:НЕ_БІЙТЕСЯ_ХЛОПЦІ_ЯКЩО_У_МЕНЕ_E3E168] 
Марк Грінвуд: Це не зовсім так працює! [ID:ЦЕ_НЕ_ЗОВСІМ_ТАК_ПРАЦЮЄ_6ED1A2] 
Олаф: У будь-якому разі, я впевнений, до цього не дійде. Тільки підкажіть, як мені звідси попасти до короля. [ID:У_БУДЬ_ЯКОМУ_РАЗІ_Я_ВПЕВНЕНИЙ_6C6A51]
: Марк дивиться на Вас розгублено, зважаючи “за” і “проти”. Поки він намагається вирішити, як зреагувати на Ваші дії, Девіс виходить вперед. [ID:МАРК_ДИВИТЬСЯ_НА_ВАС_РОЗГУБЛЕН_ACA386]
Девіс: Я вірю, ти впораєшся, Олафе. Ти вже знаєш, що тут є катакомби. Вони з’єднані у тому числі з королівським замком. Я дам карту, щоб ти не загубився. [ID:Я_ВІРЮ_ТИ_ВПОРАЄШСЯ_ОЛАФЕ_ТИ_В_02DFF3] 
Велдрік Грінвуд: Далі тгеба буде пгойди повз вагтових у замку. Для цього скогистайся таємним шляхом, я відмічу на кагті, як до нього попасти. [ID:ДАЛІ_ТГЕБА_БУДЕ_ПГОЙДИ_ПОВЗ_ВА_2A27D8] 
Марк Грінвуд: Ним не користувались вже багато років, то шлях може бути у занедбаному стані. Але ти постарайся, добре? [ID:НИМ_НЕ_КОРИСТУВАЛИСЬ_ВЖЕ_БАГАТ_1C6F6B]
: Ти киваєте братам, опускаєш очі на вимазані в іржавині пальці й проводите ними дві лінії під очима. [ID:ТИ_КИВАЄТЕ_БРАТАМ_ОПУСКАЄШ_ОЧІ_805C06]
- Запропонувати друзям звільнити їх [ID:ЗАПРОПОНУВАТИ_ДРУЗЯМ_ЗВІЛЬНИТ_A97BAF]
	: Ви підходите до клітки, де сидять Торвін, Серена й Амалія і виламуєте замки і їм. [ID:ВИ_ПІДХОДИТЕ_ДО_КЛІТКИ_ДЕ_СИДЯ_32073C]
	Олаф: Можете виходити. [ID:МОЖЕТЕ_ВИХОДИТИ_F2E474]
	Серена Морано: Та ми, мабуть таки тут залишимось… [ID:ТА_МИ_МАБУТЬ_ТАКИ_ТУТ_ЗАЛИШИМО_9EF380]
	Торвін Круторіг: Підтримую. Як розберешся з короною, і з нас знімуть звинувачення, так і вийдемо. [ID:ПІДТРИМУЮ_ЯК_РОЗБЕРЕШСЯ_З_КОРО_5C31E7] 
	Олаф: Як скажете. Тоді почекайте трохи. Скоро я визволю вас звідси. [ID:ЯК_СКАЖЕТЕ_ТОДІ_ПОЧЕКАЙТЕ_ТРОХ_D59CFD]
- Не гаяти часу [ID:НЕ_ГАЯТИ_ЧАСУ_05A049]
: Ви підходите до люка, що веде у катакомби й вибиваєте його головою, після чого стрибаєте униз. [ID:ВИ_ПІДХОДИТЕ_ДО_ЛЮКА_ЩО_ВЕДЕ_У_66C7A1]
=> END

~ escape_frivolity
: Брати понуро відвертаються. Вони й самі не раді Вам відмовляти, але не готові так сильно ризикувати. Ви засмучено сідаєте на якусь трубу, що стирчить з підлоги й важко видихаєте. [ID:БРАТИ_ПОНУРО_ВІДВЕРТАЮТЬСЯ_ВОН_092EB9] 
Олаф: Можете тоді принести вечерю? Я хоча б поїм безплатно перед смертю. [ID:МОЖЕТЕ_ТОДІ_ПРИНЕСТИ_ВЕЧЕРЮ_Я_EA68AF]
Девіс: Добре, зараз. [ID:ДОБРЕ_ЗАРАЗ_9DDF58]
: За пару хвилин Девіс повертається з тарілкою незрозумілої каші. Він просовує її через спеціальний паз у ґратах. [ID:ЗА_ПАРУ_ХВИЛИН_ДЕВІС_ПОВЕРТАЄТ_CCC0A4]
Девіс: Ось, тримай вечерю, Олафе. [ID:ОСЬ_ТРИМАЙ_ВЕЧЕРЮ_ОЛАФЕ_FCAA7B]
: Ви встаєте зі свого місця, упираєтеся рукою в сусідню трубу, яка виявляється менш надійною за ту, на якій ти сидів. Труба ламається, і з неї під сильним тиском починає хлинути вода. [ID:ВИ_ВСТАЄТЕ_ЗІ_СВОГО_МІСЦЯ_УПИР_04C85C]
Олаф: Упс. [ID:УПС_F7DA68] 
Девіс: О ні-ні-ні! [ID:О_НІ_НІ_НІ_CCDFB1]
: Велдрік і Марк підскакують до камери, намагаючись зрозуміти, що сталося. Марк реагує першим: Хапає зі скрині інструменти й кидає їх Вам у камеру. [ID:ВЕЛДРІК_І_МАРК_ПІДСКАКУЮТЬ_ДО_4F7D99]
Марк Грінвуд: Швидше полагодь, будь ласка! Ти ж можеш, правда? [ID:ШВИДШЕ_ПОЛАГОДЬ_БУДЬ_ЛАСКА_ТИ_A56CDA] 
Олаф: Хм, а ви мене випустите? [ID:ХМ_А_ВИ_МЕНЕ_ВИПУСТИТЕ_575130]
Марк Грінвуд: Що? Я ж казав, не можемо! [ID:ЩО_Я_Ж_КАЗАВ_НЕ_МОЖЕМО_9F9595]
Олаф: Ну і я тоді не можу полагодити. [ID:НУ_І_Я_ТОДІ_НЕ_МОЖУ_ПОЛАГОДИТИ_CBADEE] 
Марк Грінвуд: Аргх, добре! Випустимо! [ID:АРГХ_ДОБРЕ_ВИПУСТИМО_276B1A]
: Ви берете інструменти й починаєте лагодити трубу. [ID:ВИ_БЕРЕТЕ_ІНСТРУМЕНТИ_Й_ПОЧИНА_2F8AAA] 
Олаф: Готово! [ID:ГОТОВО_53EF36] 
: Брати стримують дане Вам слово. Марк відкриває камеру, і Ви виходите на свободу. Він кладе руку на Ваше плече. [ID:БРАТИ_СТРИМУЮТЬ_ДАНЕ_ВАМ_СЛОВО_D4544A]
Марк Грінвуд: Ми ризикуємо своїм життям, Олафе. Тож я дуже тебе прошу, зроби все, як треба! [ID:МИ_РИЗИКУЄМО_СВОЇМ_ЖИТТЯМ_ОЛАФ_1D3DFA]
Марк Грінвуд: Як ти вже знаєш, тут є катакомби. Вони з’єднані у тому числі з королівським замком. Я дам карту, щоб ти не загубився. [ID:ЯК_ТИ_ВЖЕ_ЗНАЄШ_ТУТ_Є_КАТАКОМБ_9AD2CC]
Марк Грінвуд: Далі треба буде пройди повз вартових у замку. Для цього скористайся таємним шляхом, я відмічу на карті, як до нього попасти. [ID:ДАЛІ_ТРЕБА_БУДЕ_ПРОЙДИ_ПОВЗ_ВА_BC57C4]
Марк Грінвуд: Ним не користувались вже багато років, то шлях може бути у занедбаному стані. Але ти постарайся, добре? [ID:НИМ_НЕ_КОРИСТУВАЛИСЬ_ВЖЕ_БАГАТ_1C6F6B]
Олаф: Даю слово. Торвіне, Серено, графине! Почекайте трохи. Я обов’язково визволю вас звідси! [ID:ДАЮ_СЛОВО_ТОРВІНЕ_СЕРЕНО_ГРАФИ_15D5D2]
=> END